plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.95'
}

neoForge.version = "21.6.12-beta"
version = "0.9.0"
group= "dev.gigaherz.hudcompass"
base.archivesName = "HudCompass-${neoForge.minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    // JourneyMap API releases are here
    maven {
        name = "JourneyMap (Public)"
        url = "https://jm.gserv.me/repository/maven-public/"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    flatDir { dirs 'deps' }
}

neoForge {
    runs {
        // applies to all the run configs below
        configureEach {
            gameDirectory = project.file('run')
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
        }

        client2 {
            client()

            programArguments.addAll '--username', 'gigaherz', '--user', 'gigaherz', '--uuid', '67752848-2bae-4c88-ae42-150fab05fea8'
        }

        server {
            server()
        }

        clientData {
            clientData()
            programArguments.addAll '--mod', 'hudcompass', '--all', '--output', file('src/main/generated_resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        thismod {
            sourceSet(sourceSets.main)
        }
    }

    parchment {
        minecraftVersion = "1.21.6-pre1"
        mappingsVersion = "BLEEDING-SNAPSHOT"
    }
}

dependencies {
    compileOnly 'info.journeymap:journeymap-api-neoforge:2.0.0-1.21.6-SNAPSHOT'
    //compileOnly 'flatdir:journeymap-neoforge:1.21.3-6.0.0-beta.28'
    runtimeOnly 'flatdir:journeymap-neoforge:1.21.6-6.0.0-beta.52'
}

jar {
    from(files('LICENSE.txt'))

    manifest {
        attributes([
                "Specification-Title": "hudcompass",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
